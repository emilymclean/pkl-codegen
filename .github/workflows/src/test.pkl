amends "package://github.com/emilymclean/pkl-github-actions/releases/download/v0.1.0-alpha.96/pkl-github-actions@0.1.0-alpha.96#/actions.pkl"
import "lib/base.pkl"

name = "Test Java, Kotlin, and Swift Docker images on latest version"

on = new On {
    pull_request = new PullRequest {
        branches = new Listing {
            "main"
            "develop"
        }
    }
}

jobs = new Mapping {
    ["pkl-versions"] = new Job {
        outputs = new Mapping {
            ["versions"] = "${{ env.versions }}"
        }
        steps = new Listing {
            base.checkout
            new CommandStep {
                run = """
                mapfile -t pklversions < "pkl-versions"
                json=$(jq -n -c --argjson arr "$(printf '%s\n' "${pklversions[@]}" | jq -R . | jq -s '[.[0], .[-1]]')" '{"version": $arr}')
                echo "versions=$(echo "$json")" >> ${GITHUB_ENV}
                """
            }
        }
    }
    for (platform in base.PLATFORMS) {
        ["test-\(platform)"] = (base.testPlatform(platform, "${{ matrix.version }}", false)) {
            needs = new Listing {
                "pkl-versions"
            }
            strategy = new JobStrategy {
                matrix = "${{fromJson(needs.pkl-versions.outputs.versions)}}"
            }
        }
    }
}.toMap().filter((k,v) -> v != null).toMapping()

