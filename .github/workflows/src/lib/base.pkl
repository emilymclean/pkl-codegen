open module base
extends "package://github.com/emilymclean/pkl-github-actions/releases/download/v0.1.0-alpha.96/pkl-github-actions@0.1.0-alpha.96#/actions.pkl"

hidden const PLATFORM_JAVA = "java"
hidden const PLATFORM_KOTLIN = "kotlin"
hidden const PLATFORM_SWIFT = "swift"
hidden const PLATFORM_PKLDOC = "doc"
hidden const PLATFORM_GO = "go"

hidden const RENDER_TEMPLATE_ACTION = "emilymclean/template-render-action@v1.0.2"
// https://stackoverflow.com/questions/4023830/how-to-compare-two-strings-in-dot-separated-version-format-in-bash
hidden const VERLTE = """
verlte() {
    [  "$1" = "`echo -e "$1\n$2" | sort -V | head -n1`" ]
}

verlt() {
    [ "$1" = "$2" ] && return 1 || verlte $1 $2
}
"""

typealias Platform = "java"|"kotlin"|"swift"|"doc"|"go"

hidden const PLATFORMS = new Listing {
    PLATFORM_JAVA
    PLATFORM_KOTLIN
    PLATFORM_PKLDOC 
    PLATFORM_SWIFT
    PLATFORM_GO
}

hidden supportedVersionLimit = 10

hidden platformCalls = new Mapping<Platform, String> {
    [PLATFORM_SWIFT] = "LD_LIBRARY_PATH=/usr/share/swift/usr/lib/swift/linux PATH=$PATH:/usr/share/swift/usr/bin/ /pkl-gen-swift"
    [PLATFORM_KOTLIN] = "java -cp /pklgen.jar org.pkl.codegen.kotlin.Main"
    [PLATFORM_JAVA] = "java -cp /pklgen.jar org.pkl.codegen.java.Main"
    [PLATFORM_PKLDOC] = "java -cp /pklgen.jar org.pkl.doc.Main"
}

hidden checkout = new ActionStep {
    uses = "actions/checkout@v4"
    with = new Mapping {
        ["fetch-depth"] = 0
    }
}

hidden currentVersion: Step = new CommandStep {
    name = "Get current latest supported pkl version"
    run = """
    mapfile -t pklversions < "pkl-versions"
    echo "PKL_VERSION=${pklversions[-1]}" >> $GITHUB_ENV
    """
}

function dockerBuild(withM: Mapping, ifStatement: String?): Listing<Step> = new Listing {
    new ActionStep {
        name = "Build and push Docker image"
        uses = "docker/build-push-action@v6"
        `if` = ifStatement
        with = withM
    }
}

function dockerSetup(): Listing<Step> = new Listing {
    new ActionStep {
        name = "Setup QEMU"
        uses = "docker/setup-qemu-action@v3"
    }
    new ActionStep {
        name = "Setup Docker Buildx"
        uses = "docker/setup-buildx-action@v3"
    }
}

hidden dockerBuild: ActionStep = new ActionStep {
    name = "Log in to the Container registry"
    uses = "docker/login-action@v3"
}

function renderForPlatform(platform: Platform, version: String): Listing<Step> = new Listing {
    // Please apple, let me use list expansion as the result of an if
    if (platform == PLATFORM_SWIFT)
        new CommandStep {
            name = "Read swift-tool-version"
            run = """
            \(VERLTE)

            echo "swift_tool_version=$(verlt \(version) 0.29.0 && echo "0.4.2" || cat swift/swift-tool-version)" >> $GITHUB_ENV
            """
        }
    else null
    if (platform == PLATFORM_GO)
        new CommandStep {
            name = "Read go-tool-version"
            run = """
            \(VERLTE)

            echo "go_tool_version=$(verlt \(version) 0.29.0 && echo "0.10.0" || cat go/go-tool-version)" >> $GITHUB_ENV
            """
        }
    else null
    if (List(PLATFORM_GO).contains(platform))
        new ActionStep {
            name = "Render template.Dockerfile"
            uses = RENDER_TEMPLATE_ACTION
            env = new Mapping {
                ["pkl_version"] = version
            }
            with = new Mapping {
                ["template-file"] = "\(platform)/template.Dockerfile"
                ["output-file"] = "\(platform)/Dockerfile"
                ["engine"] = "mustache"
            }
        }
    else null
    if (List(PLATFORM_GO, PLATFORM_SWIFT).contains(platform))
        new ActionStep {
            name = "Render setup.template.sh"
            uses = RENDER_TEMPLATE_ACTION
            env = new Mapping {
                ["pkl_version"] = version
            }
            with = new Mapping {
                ["template-file"] = "\(platform)/setup.template.sh"
                ["output-file"] = "\(platform)/setup.sh"
                ["engine"] = "mustache"
            }
        }
    else null
    if (List(PLATFORM_GO, PLATFORM_SWIFT).contains(platform))
        new CommandStep {
            name = "Make setup.sh executable"
            run = "chmod +x \(platform)/setup.sh"
        }
    else null
    if (!List(PLATFORM_GO, PLATFORM_SWIFT).contains(platform))
        new ActionStep {
            name = "Render build.template.gradle"
            uses = RENDER_TEMPLATE_ACTION
            env = new Mapping {
                ["pkl_version"] = version
            }
            with = new Mapping {
                ["template-file"] = "\(platform)/project/build.template.gradle"
                ["output-file"] = "\(platform)/project/build.gradle"
                ["engine"] = "mustache"
            }
        }
    else null
    if (platform != PLATFORM_GO)
        new ActionStep {
            name = "Render endpoint.sh"
            uses = RENDER_TEMPLATE_ACTION
            env = new Mapping {
                ["call"] = platformCalls[platform]
            }
            with = new Mapping {
                ["template-file"] = "templates/entrypoint.template.sh"
                ["output-file"] = "\(platform)/entrypoint.sh"
                ["engine"] = "mustache"
            }
        }
    else null
    if (platform != PLATFORM_GO)
        new CommandStep {
            name = "Make endpoint.sh executable"
            run = "chmod +x \(platform)/entrypoint.sh"
        }
    else null
}.toList().filterNonNull().toListing()

function buildArgsForPlatform(platform: Platform, version: String): Listing<Step> = new Listing {
    if (platform == PLATFORM_SWIFT)
        new CommandStep {
            name = "Set swift image version"
            run = """
            \(VERLTE)

            echo "BUILD_ARGS=$(verlt \(version) 0.29.0 && echo "SWIFT_VERSION=5.10" || echo "SWIFT_VERSION=6.1.3")" >> $GITHUB_ENV
            """
        }
    else null
}.toList().filterNonNull().toListing()

local function testString(platform: String, extraFiles: List<String>): String = """
set -e
# Test using new style explicitly listing -o
docker run --rm -v "./test-data/:/data" test/\(platform):latest /data/example.pkl \(if(extraFiles.length > 0) extraFiles.join(" ") else "") -o /data/newstyle-out
# Test using new style explicitly listing --output-dir
docker run --rm -v "./test-data/:/data" test/\(platform):latest /data/example.pkl \(if(extraFiles.length > 0) extraFiles.join(" ") else "") --output-dir /data/newstyle-full-out
# Test using old style without -o
docker run --rm -v "./test-data/:/data" test/\(platform):latest /data/example.pkl \(if(extraFiles.length > 0) extraFiles.join(" ") else "") /data/oldstyle-out

ls test-data
echo "Output folder contents:"
ls test-data/newstyle-out
echo "Output Full folder contents:"
ls test-data/newstyle-full-out
"""

function testPlatform(platform: String, version: String, upload: Boolean): Job = new Job {
    steps = new Listing {
        checkout
        currentVersion
        ...renderForPlatform(platform, version)
        ...buildArgsForPlatform(platform, version)
        ...dockerSetup()
        if (platform == PLATFORM_SWIFT)
            new CommandStep {
                name = "Build Docker image"
                run = "docker buildx build \(platform)/ --build-arg ${{ env.BUILD_ARGS }} -t test/\(platform):latest --load"
            }
        else
            new CommandStep {
                name = "Build Docker image"
                run = "docker buildx build \(platform)/ -t test/\(platform):latest --load"
            }
        ...new Listing {
            if (platform == PLATFORM_PKLDOC)
                new CommandStep {
                    name = "Generate and test"
                    run = testString(platform, List("\"/data/example space.pkl\"", "/data/doc-package-info.pkl"))
                }
            else if (platform == PLATFORM_JAVA) 
                new CommandStep {
                    name = "Generate and test"
                    run = testString(platform, List("--generate-getters", "/data/test.pkl"))
                }
            else if (platform == PLATFORM_GO)
                new CommandStep {
                    name = "Generate and test"
                    run = """
                    docker run --rm -v "./test-data/:/data" test/\(platform):latest /data/example.pkl --mapping example=github.com/emilymclean/pkl-codegen/example --output-path /data/newstyle-out

                    echo "Output folder contents:"
                    ls test-data/newstyle-out
                    """
                }
            else
                new CommandStep {
                    name = "Generate and test"
                    run = testString(platform, List("\"/data/example space.pkl\""))
                }
            new CommandStep {
                name = "Test if any output"
                run = """
                set -f
                OUTPUT=$(ls -A "test-data/newstyle-out")
                set +f

                if [ -z "$OUTPUT" ]; then
                    echo "No output in newstyle-out, failing"
                    exit 1
                fi
                """
            }
            ...(
                if (upload) 
                    new Listing {
                        new ActionStep {
                            uses = "actions/upload-artifact@v4"
                            with = new Mapping {
                                ["name"] = "\(platform)-output"
                                ["path"] = "test-data/newstyle-out/"
                            }
                        }
                    }
                else new Listing {}
            )
        }
    }
}

// Would love to have this as a matrix but that is annoying to read
function wasUpdated(dir: String): Job = new Job {
    outputs = new Mapping {
        ["any-changed"] = "${{ env.any_changed }}"
    }
    steps = new Listing {
        checkout
        new CommandStep {
            run = """
            if git diff --no-commit-id --name-only HEAD HEAD~1 | grep -Eq '(\(dir)/.*|templates/entrypoint.template.sh|pkl-versions|.github/workflows/build.yml)'; then
                echo "any_changed=true" >> "$GITHUB_ENV"
            else
                echo "any_changed=false" >> "$GITHUB_ENV"
            fi
            """
        }
    }
}

hidden pklVersionsJob = new Job {
    outputs = new Mapping {
        ["versions"] = "${{ env.versions }}"
    }
    steps = new Listing {
        checkout
        new CommandStep {
            run = """
            mapfile -t pklversions < "pkl-versions"
            json=$(jq -n -c --argjson arr "$(printf '%s\n' "${pklversions[@]}" | jq -R . | jq -s .)" '{"version": $arr}')
            echo "versions=$(echo "$json")" >> ${GITHUB_ENV}
            """
        }
    }
}