amends "package://github.com/emilymclean/pkl-github-actions/releases/download/v0.1.0-alpha.96/pkl-github-actions@0.1.0-alpha.96#/actions.pkl"
import "lib/base.pkl"

local childRepositories = new Listing {
    "pkl-java-action"
    "pkl-swift-action"
    "pkl-kotlin-action"
    "pkldoc-action"
}

local dockerPermissions = new Mapping {
    ["contents"] = "read"
    ["packages"] = "write"
}

local function baseBuildJob(platform: String, login: Step, reference: String): Job = new Job {
    needs = new Listing {
        "version"
        "pkl-versions"
        "was-updated-\(platform)"
    }
    `if` = "needs.was-updated-\(platform).outputs.any-changed == 'true'"
    strategy = new JobStrategy {
        matrix = "${{fromJson(needs.pkl-versions.outputs.versions)}}"
    }
    permissions = dockerPermissions
    steps = new Listing {
        base.checkout
        ...base.renderForPlatform(platform, "${{ matrix.version }}")
        ...base.buildArgsForPlatform(platform, "${{ matrix.version }}")
        ...base.dockerSetup()
        login
        ...base.dockerBuild(
            new Mapping {
                ["context"] = platform
                ["platforms"] = "linux/amd64,linux/arm64/v8"
                ["push"] = true
                ["tags"] = """
                \(reference):${{ matrix.version }}
                \(reference):${{ matrix.version }}-nightly
                \(reference):${{ matrix.version }}-${{ needs.version.outputs.major }}-nightly
                \(reference):${{ matrix.version }}-${{ needs.version.outputs.version }}
                \(reference):${{ matrix.version }}-${{ needs.version.outputs.major }}
                """
                ["build-args"] = "${{ env.BUILD_ARGS }}"
            },
            "github.ref == 'refs/heads/main'"
        )
        ...base.dockerBuild(
            new Mapping {
                ["context"] = platform
                ["platforms"] = "linux/amd64,linux/arm64/v8"
                ["push"] = true
                ["tags"] = """
                \(reference):${{ matrix.version }}-nightly
                \(reference):${{ matrix.version }}-${{ needs.version.outputs.major }}-nightly
                \(reference):${{ matrix.version }}-${{ needs.version.outputs.version }}
                """
                ["build-args"] = "${{ env.BUILD_ARGS }}"
            },
            "github.ref != 'refs/heads/main'"
        )
    }
}

local function buildJob(platform: String): Mapping<String, Job> = new Mapping {
    ["build-\(platform)-ghcr"] = baseBuildJob(
        platform,  
        (base.dockerBuild) {
            with = new Mapping {
                ["registry"] = "https://ghcr.io"
                ["username"] = "${{ github.actor }}"
                ["password"] = "${{ secrets.GITHUB_TOKEN }}"
            }
        },
        "${{ vars.LOCAL_DOCKER_REFERENCE }}-\(platform)"
    )
    ["build-\(platform)-dockerio"] = baseBuildJob(
        platform,  
        (base.dockerBuild) {
            with = new Mapping {
                ["username"] = "${{ vars.DOCKER_USERNAME }}"
                ["password"] = "${{ secrets.DOCKER_PAT }}"
            }
        },
        "${{ vars.DOCKER_DOCKER_REFERENCE }}-\(platform)"
    )
}

name = "Build Java, Kotlin, Swift, and PklDoc Docker images"

on = new On {
    push = new Push {
        branches = new Listing {
            "main"
            "develop"
        }
    }
}

permissions = new Mapping {
    ["contents"] = "write"
}

jobs = new Mapping {
    ["version"] = new Job {
        outputs = new Mapping {
            ["version"] = "${{ steps.gitversion.outputs.semVer }}"
            ["major"] = "${{ steps.gitversion.outputs.major }}"
        }
        steps = new Listing {
            base.checkout
            new ActionStep {
                name = "Install GitVersion"
                uses = "gittools/actions/gitversion/setup@v3.0.0"
                with = new Mapping {
                    ["versionSpec"] = "5.x"
                }
            }
            new ActionStep {
                name = "Use GitVersion"
                id = "gitversion"
                uses = "gittools/actions/gitversion/execute@v3.0.0"
                with = new Mapping {
                    ["useConfigFile"] = true
                    ["configFilePath"] = "./gitversion.yml"
                }
            }
            new CommandStep {
                name = "Generate Job Summary"
                run = "echo \"# Version Info\nSemantic Version: ${{ steps.gitversion.outputs.semVer }}\" >> $GITHUB_STEP_SUMMARY"
            }
        }
    }
    ["release"] = new Job {
        needs = new Listing {
            "version"
        }
        `if` = "github.event_name != 'pull_request'"
        permissions = new Mapping {
            ["contents"] = "write"
        }
        outputs = new Mapping {
            ["upload_url"] = "${{ steps.create_release.outputs.upload_url }}"
        }
        steps = new Listing {
            base.checkout
            new ActionStep {
                name = "Create tag for release"
                uses = "rickstaa/action-create-tag@v1"
                with = new Mapping {
                    ["tag"] = "v${{ needs.version.outputs.version }}"
                }
            }
            new ActionStep {
                id = "create_release"
                uses = "softprops/action-gh-release@v2"
                env = new Mapping {
                    ["GITHUB_TOKEN"] = "${{ secrets.GITHUB_TOKEN }}"
                }
                with = new Mapping {
                    ["tag_name"] = "v${{ needs.version.outputs.version }}"
                    ["name"] = "Release ${{ needs.version.outputs.version }}"
                    ["prerelease"] = "${{ github.ref == 'refs/heads/develop' }}"
                }
            }
        }
    }
    ["pkl-versions"] = base.pklVersionsJob
    for (platform in base.PLATFORMS) {
        ["was-updated-\(platform)"] = base.wasUpdated(platform)
    }
    for (platform in base.PLATFORMS) {
        ...buildJob(platform)
    }
    ["update-pkl-actions"] = new Job {
        `if` = "github.ref == 'refs/heads/main' && github.repository_owner == 'emilymclean'"
        needs = new Listing {
            for (platform in base.PLATFORMS) {
                "build-\(platform)-ghcr"
            }
        }
        strategy = new JobStrategy {
            matrix = new Mapping {
                ["repo"] = childRepositories
            }
        }
        steps = new Listing {
            base.checkout
            base.currentVersion
            new ActionStep {
                name = "Update pkl actions that use these images"
                uses = "benc-uk/workflow-dispatch@v1"
                with = new Mapping {
                    ["workflow"] = "update-pkl.yml"
                    ["repo"] = "emilymclean/${{ matrix.repo }}"
                    ["token"] = "${{ secrets.WORKER_PAT }}"
                    ["inputs"] = "{ \"pkl-version\": \"${{ env.PKL_VERSION }}\" }"
                }
            }
        }
    }
}.toMap().filter((k,v) -> v != null).toMapping()