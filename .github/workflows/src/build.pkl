amends "package://github.com/emilymclean/pkl-github-actions/releases/download/v0.1.0-alpha.96/pkl-github-actions@0.1.0-alpha.96#/actions.pkl"
import "lib/base.pkl"

local childRepositories = new Listing {
    "pkl-java-action"
    "pkl-swift-action"
    "pkl-kotlin-action"
    "pkldoc-action"
}

local dockerPermissions = new Mapping {
    ["contents"] = "read"
    ["packages"] = "write"
}

// Would love to have this as a matrix but that is annoying to read
local function wasUpdated(dir: String): Job = new Job {
    outputs = new Mapping {
        ["any-changed"] = "${{ steps.changed.outputs.any_changed }}"
    }
    steps = new Listing {
        base.checkout
        new ActionStep {
            id = "changed"
            uses = "tj-actions/changed-files@v45"
            with = new Mapping {
                ["files"] = """
                \(dir)/**
                templates/entrypoint.template.sh
                pkl-versions
                """
            }
        }
    }
}

local function buildJob(dir: String): Job = new Job {
    needs = new Listing {
        "version"
        "pkl-versions"
        "was-updated-\(dir)"
    }
    `if` = "needs.was-updated-\(dir).outputs.any-changed == 'true'"
    strategy = new JobStrategy {
        matrix = "${{fromJson(needs.pkl-versions.outputs.versions)}}"
    }
    permissions = dockerPermissions
    steps = new Listing {
        base.checkout
        ...base.renderForPlatform(dir, "${{ matrix.version }}")
        ...docker(dir)
    }
}

local function docker(dir: String): Listing =
    new Listing {
        new CommandStep {
            name = "Lowercase repository name"
            run = """
            echo "REPOSITORY_LC=${REPOSITORY,,}" >> ${GITHUB_ENV}
            """
            env = new Mapping {
                ["REPOSITORY"] = "${{ github.repository }}"
            }
        }
        ...base.dockerSetup()
        ...base.dockerBuild(
            new Mapping {
                ["context"] = dir
                ["platforms"] = "linux/amd64,linux/arm64/v8"
                ["push"] = true
                ["tags"] = """
                ghcr.io/${{ env.REPOSITORY_LC }}-\(dir):${{ matrix.version }}
                ghcr.io/${{ env.REPOSITORY_LC }}-\(dir):${{ matrix.version }}-nightly
                ghcr.io/${{ env.REPOSITORY_LC }}-\(dir):${{ matrix.version }}-${{ needs.version.outputs.major }}-nightly
                ghcr.io/${{ env.REPOSITORY_LC }}-\(dir):${{ matrix.version }}-${{ needs.version.outputs.version }}
                ghcr.io/${{ env.REPOSITORY_LC }}-\(dir):${{ matrix.version }}-${{ needs.version.outputs.major }}
                """
            },
            "github.ref == 'refs/heads/main'"
        )
        ...base.dockerBuild(
            new Mapping {
                ["context"] = dir
                ["platforms"] = "linux/amd64,linux/arm64/v8"
                ["push"] = true
                ["tags"] = """
                ghcr.io/${{ env.REPOSITORY_LC }}-\(dir):${{ matrix.version }}-nightly
                ghcr.io/${{ env.REPOSITORY_LC }}-\(dir):${{ matrix.version }}-${{ needs.version.outputs.major }}-nightly
                ghcr.io/${{ env.REPOSITORY_LC }}-\(dir):${{ matrix.version }}-${{ needs.version.outputs.version }}
                """
            },
            "github.ref != 'refs/heads/main'"
        )
     }

name = "Build Java, Kotlin, Swift, and PklDoc Docker images"

on = new On {
    push = new Push {
        branches = new Listing {
            "main"
            "develop"
        }
    }
}

permissions = new Mapping {
    ["contents"] = "write"
}

jobs = new Mapping {
    ["version"] = new Job {
        outputs = new Mapping {
            ["version"] = "${{ steps.gitversion.outputs.semVer }}"
            ["major"] = "${{ steps.gitversion.outputs.major }}"
        }
        steps = new Listing {
            base.checkout
            new ActionStep {
                name = "Install GitVersion"
                uses = "gittools/actions/gitversion/setup@v3.0.0"
                with = new Mapping {
                    ["versionSpec"] = "5.x"
                }
            }
            new ActionStep {
                name = "Use GitVersion"
                id = "gitversion"
                uses = "gittools/actions/gitversion/execute@v3.0.0"
                with = new Mapping {
                    ["useConfigFile"] = true
                    ["configFilePath"] = "./gitversion.yml"
                }
            }
            new CommandStep {
                name = "Generate Job Summary"
                run = "echo \"# Version Info\nSemantic Version: ${{ steps.gitversion.outputs.semVer }}\" >> $GITHUB_STEP_SUMMARY"
            }
        }
    }
    ["release"] = new Job {
        needs = new Listing {
            "version"
        }
        `if` = "github.event_name != 'pull_request'"
        permissions = new Mapping {
            ["contents"] = "write"
        }
        outputs = new Mapping {
            ["upload_url"] = "${{ steps.create_release.outputs.upload_url }}"
        }
        steps = new Listing {
            base.checkout
            new ActionStep {
                id = "create_release"
                uses = "softprops/action-gh-release@v2"
                env = new Mapping {
                    ["GITHUB_TOKEN"] = "${{ secrets.GITHUB_TOKEN }}"
                }
                with = new Mapping {
                    ["tag_name"] = "v${{ needs.version.outputs.version }}"
                    ["name"] = "Release ${{ needs.version.outputs.version }}"
                    ["prerelease"] = "${{ github.ref == 'refs/heads/develop' }}"
                }
            }
        }
    }
    ["pkl-versions"] = new Job {
        outputs = new Mapping {
            ["versions"] = "${{ env.versions }}"
        }
        steps = new Listing {
            base.checkout
            new CommandStep {
                run = """
                mapfile -t pklversions < "pkl-versions"
                json=$(jq -n -c --argjson arr "$(printf '%s\n' "${pklversions[@]}" | jq -R . | jq -s .)" '{"version": $arr}')
                echo "versions=$(echo "$json")" >> ${GITHUB_ENV}
                """
            }
        }
    }
    for (platform in base.PLATFORMS) {
        ["was-updated-\(platform)"] = wasUpdated(platform)
    }
    for (platform in base.PLATFORMS) {
        ["build-\(platform)"] = buildJob(platform)
    }
    ["update-pkl-actions"] = new Job {
        `if` = "github.ref == 'refs/heads/main' && github.repository_owner == 'emilymclean'"
        needs = new Listing {
            for (platform in base.PLATFORMS) {
                "build-\(platform)"
            }
        }
        strategy = new JobStrategy {
            matrix = new Mapping {
                ["repo"] = childRepositories
            }
        }
        steps = new Listing {
            base.checkout
            base.currentVersion
            new ActionStep {
                name = "Update pkl actions that use these images"
                uses = "benc-uk/workflow-dispatch@v1"
                with = new Mapping {
                    ["workflow"] = "update-pkl.yml"
                    ["repo"] = "emilymclean/${{ matrix.repo }}"
                    ["token"] = "${{ secrets.WORKER_PAT }}"
                    ["inputs"] = "{ \"pkl-version\": \"${{ env.PKL_VERSION }}\" }"
                }
            }
        }
    }
}.toMap().filter((k,v) -> v != null).toMapping()